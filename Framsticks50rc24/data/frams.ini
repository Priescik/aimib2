function init()
{
	CLI.addMacro("lg", 0, "listgenotypes(0);", "list genotypes from group 0");
	CLI.addMacro("lg", 1, "listgenotypes($1);", "list genotypes from any group");
	CLI.addMacro("lg", 2, "listgenotypes_format($1,\"$2\");", "list genotypes from any group, using custom format");
	CLI.addMacro("lc", 0, "listcreatures(0);", "list creatures from group 0");
	CLI.addMacro("lc", 1, "listcreatures($1);", "list creatures from any group");
	CLI.addMacro("lc", 2, "listcreatures_format($1,\"$2\");", "list creatures from any group, using custom format");
	CLI.addMacro("pg", 0, "printobject(GenePools[0][0]);", "print the first genotype from group 0");
	CLI.addMacro("pg", 1, "printobject(GenePools[0][$1]);", "print a genotype from group 0");
	CLI.addMacro("pg", 2, "printobject(GenePools[$1][$2]);", "print a genotype from any group");
	CLI.addMacro("pc", 0, "printobject(Populations[0][0]);", "print the first creature from group 0");
	CLI.addMacro("pc", 1, "printobject(Populations[0][$1]);", "print a creature from group 0");
	CLI.addMacro("pc", 2, "printobject(Populations[$1][$2]);", "print a creature from any group");
	//CLI.addMacro("lgg",0,"....listing genotype groups...","list genotype groups");
	CLI.addMacro("help", 0, "shorthelp();", "display short help message");
	CLI.addMacro("qu", 0, "CLI.quit();", "leave comand shell");
	CLI.addMacro("lm", 0, "CLI.listMacros();", "list command-line macros");
	CLI.addMacro("st", 0, "Simulator.start();Simulator.step();Simulator.stop();", "do a single simulation step");
	CLI.addMacro("st", 1, "Simulator.start(); var i;for(i=0;(i<$1)&&Simulator.running;i++) Simulator.step(); Simulator.stop();", "do N simulation steps");
	CLI.addMacro("go", 0, "Simulator.start();while(Simulator.running) Simulator.step();", "simulate until interrupted");
	CLI.addMacro("lo", 1, "Simulator.load(\"$1\");", "load experiment state from file (*.expt)");
	CLI.addMacro("im", 1, "Simulator.import(\"$1\");", "import experiment data and/or settings from file (*.gen/*.sim/*.expt)");
	CLI.addMacro("sa", 1, "Simulator.save(\"$1\");", "save experiment state to file (*.expt)");
	CLI.addMacro("expdef", 1, "Simulator.expdef=\"$1\";", "set the experiment definition");
	CLI.addMacro("??", 1, "search(\"$1\");", "search classes information");
	CLI.addMacro("??", 0, "aftersearch();", "show last search results again");
	CLI.addMacro("?", -1, "CLI.println($0);", "evaluate expression");

	if (!CLI.quiet)
	{
		CLI.println("---------------------------------------------------");
		shorthelp();
	}
}

function shorthelp()
{
	CLI.println("Basic commands:");
	CLI.println(" - help      : show this help message");
	CLI.println(" - qu or EOF : leave command shell");
	CLI.println(" - lo FILENAME  : load experiment, genotypes, settings (expt/gen/sim)");
	CLI.println(" - sa FILENAME  : save experiment (expt)");
	CLI.println(" - st        : do single simulation step");
	CLI.println(" - st NNN    : do NNN simulation steps");
	CLI.println(" - go        : simulate until interrupted (ctrl+c)");
	CLI.println(" - lm        : list all commandline macros");
}

function search(word)
{
	var num = String.parseNumber(word);
	if (num != null)
	{
		showresult( -1 + num);
		return ;
	}
	ClassBrowser.search(word);
	aftersearch();
}

function aftersearch()
{
	var n = ClassBrowser.resultCount;
	if (n == 0)
	{
		CLI.println("not found!");
		return ;
	}
	if (n == 1)
		showresult(0);
	else
	{
		var i;
		for (i = 0; i < n; i++)
			CLI.println("" + (i + 1) + ". " + ClassBrowser.getLabel(i));
		CLI.println("  use ?? NUMBER to show item details");
	}
}

function showresult(which)
{
	if (which < 0)
		return ;
	if (which >= ClassBrowser.resultCount)
		return ;
	CLI.println(ClassBrowser.getText(which));
}

function listgenotypes(group)
{
	listgenotypes_format(group, "name/25,numparts/8,numneurons/8,instances/6,lifespan/8,velocity/8,fit2/8");
}

function listgenotypes_format(group, fmt)
{
	var gr = GenePools[group];
	if (gr.size == 0)
	{
		CLI.println("Group " + group + " is empty");
		return ;
	}
	RowFormat.clear();
	RowFormat.prepare(Genotype.*, fmt);
	CLI.println("%s(%d):" % gr.name % gr.size);
	CLI.println("  #  " + RowFormat.header);
	for (var g in gr)
		CLI.println("%3d. %s" % g.index % RowFormat.format(g));
}

function listcreatures(group)
{
	listcreatures_format(group, "name/25,energy/8,perf_measuring/8,nn_active/6,lifespan/8,velocity/8");
}

function listcreatures_format(group, fmt)
{
	var gr = Populations[group];
	if (gr.size == 0)
	{
		CLI.println("Group " + group + " is empty");
		return ;
	}
	RowFormat.clear();
	RowFormat.prepare(Creature.*, fmt);
	CLI.println("%s(%d):" % gr.name % gr.size);
	CLI.println("  #  " + RowFormat.header);
	for (var c in gr)
		CLI.println("%3d. %s" % c.index % RowFormat.format(c));
}

function printobject(obj)
{
	if (obj == null) return;
	var i = Interface.makeFrom(obj);
	for(var p = 0; p < i.size; p++)
		if ((String.substr(i.getType(p), 0, 1) != "p") && ((i.getFlags(p) & 8192) == 0)) //exclude "p" type and deprecated
			CLI.println(i.getId(p) + ": " + i.get(p));
}
