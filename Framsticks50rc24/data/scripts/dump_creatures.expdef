expdef:
name:Dump creatures
info:~
In each simulation step, it dumps coordinates and velocities of all Parts of all living Creatures from population #0.
See the "dump.txt" file in the "scripts_output" subdirectory.
~
code:~

function onExpDefLoad()
{
	World.wrldsiz = 30; // comfortable size to play interactively with creatures
	World.wrldbnd = 2; // teleport (wraparound) to avoid creatures going far away (avoid unbounded coordinate values)
}

@include "standard_placement.inc"

function onBorn(cr)
{
	const TRIALS = 50; //try to find free place 50 times
	var retry = TRIALS;
	var placed_ok = 0;
	while (retry--)
	{
		placeCreatureRandomly(cr, 0, 0); //random place, no rotation
		if (!cr.boundingBoxCollisions(0))
		{
			placed_ok = 1;
			break;
		}
	}
	if (!placed_ok)
		Simulator.message("onBorn() could not avoid collisions.", 1);
}


@include "dump_creatures.inc"

function onStep()
{
	dumpCreatures(1, 1); //the second argument selects the file format; see dump_creatures.inc
}
~

property:
id:creath
name:Initial elevation
type:f -1 50 0.1
help:~
Vertical position (above the surface) where newborn creatures are placed.
Negative values are only used in the water area:
  0   = at the surface
-0.5 = half depth
-1   = just above the bottom~

